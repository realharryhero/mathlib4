import Mathlib.Algebra.Algebra.Unitization
import Mathlib.Analysis.NormedSpace.OperatorNorm.Mul
import Mathlib.Analysis.NormedSpace.ProdLp

variable (ğ•œ A : Type*) [NormedField ğ•œ] [NonUnitalNormedRing A]
variable [NormedSpace ğ•œ A] [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A]

namespace WithLp

open Unitization

noncomputable def unitization_addEquiv_prod : WithLp 1 (Unitization ğ•œ A) â‰ƒ+ WithLp 1 (ğ•œ Ã— A) :=
  (WithLp.linearEquiv 1 ğ•œ (Unitization ğ•œ A)).toAddEquiv.trans <|
    (addEquiv ğ•œ A).trans (WithLp.linearEquiv 1 ğ•œ (ğ•œ Ã— A)).symm.toAddEquiv

noncomputable instance instUnitizationNormedAddCommGroup :
    NormedAddCommGroup (WithLp 1 (Unitization ğ•œ A)) :=
  NormedAddCommGroup.induced (WithLp 1 (Unitization ğ•œ A)) (WithLp 1 (ğ•œ Ã— A))
    (unitization_addEquiv_prod ğ•œ A) (AddEquiv.injective _)

theorem uniformEmbedding_unitization_addEquiv_prod :
    UniformEmbedding (unitization_addEquiv_prod ğ•œ A) where
  comap_uniformity := rfl
  inj := (unitization_addEquiv_prod ğ•œ A).injective

/-- `Unitization ğ•œ A` is complete whenever `ğ•œ` and `A` are also.  -/
instance instCompleteSpace [CompleteSpace ğ•œ] [CompleteSpace A] :
    CompleteSpace (WithLp 1 (Unitization ğ•œ A)) :=
  (completeSpace_congr (uniformEmbedding_unitization_addEquiv_prod ğ•œ A)).mpr CompleteSpace.prod

variable {ğ•œ A}

open ENNReal in
lemma unitization_norm_def (x : WithLp 1 (Unitization ğ•œ A)) :
    â€–xâ€– = â€–(WithLp.equiv 1 _ x).fstâ€– + â€–(WithLp.equiv 1 _ x).sndâ€– := calc
  â€–xâ€– = (â€–(WithLp.equiv 1 _ x).fstâ€– ^ (1 : â„â‰¥0âˆ).toReal +
      â€–(WithLp.equiv 1 _ x).sndâ€– ^ (1 : â„â‰¥0âˆ).toReal) ^ (1 / (1 : â„â‰¥0âˆ).toReal) :=
    WithLp.prod_norm_eq_add (by simp : 0 < (1 : â„â‰¥0âˆ).toReal) _
  _   = â€–(WithLp.equiv 1 _ x).fstâ€– + â€–(WithLp.equiv 1 _ x).sndâ€– := by simp

lemma unitization_nnnorm_def (x : WithLp 1 (Unitization ğ•œ A)) :
    â€–xâ€–â‚Š = â€–(WithLp.equiv 1 _ x).fstâ€–â‚Š + â€–(WithLp.equiv 1 _ x).sndâ€–â‚Š :=
  Subtype.ext <| unitization_norm_def x

instance instUnitizationRing : Ring (WithLp 1 (Unitization ğ•œ A)) :=
  inferInstanceAs (Ring (Unitization ğ•œ A))

@[simp]
lemma unitization_mul (x y : WithLp 1 (Unitization ğ•œ A)) :
    WithLp.equiv 1 _ (x * y) = (WithLp.equiv 1 _ x) * (WithLp.equiv 1 _ y) :=
  rfl

-- should generalize this and the next two lemmas further to other `SMul` instances and whatnot.
instance instUnitizationAlgebra : Algebra ğ•œ (WithLp 1 (Unitization ğ•œ A)) :=
  inferInstanceAs (Algebra ğ•œ (Unitization ğ•œ A))

lemma unitization_smul (r : ğ•œ) (x : WithLp 1 (Unitization ğ•œ A)) :
    WithLp.equiv 1 _ (r â€¢ x) = r â€¢ (WithLp.equiv 1 _ x) :=
  rfl

@[simp]
lemma unitization_algebraMap (r : ğ•œ) :
    WithLp.equiv 1 _ (algebraMap ğ•œ (WithLp 1 (Unitization ğ•œ A)) r) =
      algebraMap ğ•œ (Unitization ğ•œ A) r :=
  rfl

noncomputable instance instUnitizationNormedRing : NormedRing (WithLp 1 (Unitization ğ•œ A)) where
  dist_eq := dist_eq_norm
  norm_mul x y := by
    simp_rw [unitization_norm_def, add_mul, mul_add, unitization_mul, fst_mul, snd_mul]
    rw [add_assoc, add_assoc]
    gcongr
    Â· exact norm_mul_le _ _
    Â· apply (norm_add_le _ _).trans
      gcongr
      Â· simp [norm_smul]
      Â· apply (norm_add_le _ _).trans
        gcongr
        Â· simp [norm_smul, mul_comm]
        Â· exact norm_mul_le _ _

noncomputable instance instUnitizationNormedAlgebra :
    NormedAlgebra ğ•œ (WithLp 1 (Unitization ğ•œ A)) where
  norm_smul_le r x := by
    simp_rw [unitization_norm_def, unitization_smul, fst_smul, snd_smul, norm_smul, mul_add]
    exact le_rfl

end WithLp
